Import(["env", "conf_env"])

if env["SCONS_STAGE"] == "build" :
	myenv = env.Clone()
	myenv.UseFlags(env["LUA_FLAGS"])
	myenv.UseFlags(env["SWIFTEN_FLAGS"])
	myenv.UseFlags(env["SWIFTEN_DEP_FLAGS"])
	myenv["SHLIBPREFIX"] = ""

	if myenv["PLATFORM"] == "win32" :
		myenv.Append(CPPDEFINES = ["SLUIFT_BUILD_DLL"])
	elif myenv["PLATFORM"] == "darwin" :
		myenv["SHLIBSUFFIX"] = ".so"

	myenv.SharedLibrary("sluift", [
			"sluift.cpp",
		])

	sluift_bin_env = myenv.Clone()
	sluift_bin_env.Install(".", "#/3rdParty/Lua/src/lua.c")
	if sluift_bin_env.get("HAVE_READLINE", False) :
		sluift_bin_env.Append(CPPDEFINES = ["LUA_USE_READLINE"])
		sluift_bin_env.MergeFlags(sluift_bin_env["READLINE_FLAGS"])
	sluift_bin_env.Program("sluift", [
			"sluift.cpp",
			"lua.c",
			"linit.c",
		])
