import os

Import("env")

# The checker
checker_env = env.Clone()
checker_env.MergeFlags(env["CPPUNIT_FLAGS"])
checker_env.Library("Checker", "checker.cpp")
env["CHECKER_FLAGS"] = { 
    "LIBS": ["Checker"],
    "LIBPATH": [Dir(".")],
		"LINKFLAGS": ["/SUBSYSTEM:CONSOLE"] if env["PLATFORM"] == "win32" else []
  }

if "check" in COMMAND_LINE_TARGETS or env.GetOption("clean") :
	myenv = env.Clone()
	myenv.MergeFlags(env["CHECKER_FLAGS"])
	myenv.MergeFlags(env["SLIMBER_FLAGS"])
	myenv.MergeFlags(env["SWIFT_CONTROLLERS_FLAGS"])
	myenv.MergeFlags(env["SWIFTEN_FLAGS"])
	myenv.MergeFlags(env["CPPUNIT_FLAGS"])
	myenv.MergeFlags(env["LIBIDN_FLAGS"])
	myenv.MergeFlags(env["BOOST_FLAGS"])
	myenv.MergeFlags(env["SQLITE_FLAGS"])
	myenv.MergeFlags(env.get("LIBXML_FLAGS", ""))
	myenv.MergeFlags(env.get("EXPAT_FLAGS", ""))
	myenv.MergeFlags(env["ZLIB_FLAGS"])
	if env.get("HAVE_LIBXML") :
		myenv.Append(CPPDEFINES = ["HAVE_LIBXML"])
	if env.get("HAVE_EXPAT") :
		myenv.Append(CPPDEFINES = ["HAVE_EXPAT"])
	checker = myenv.Program("checker", env["UNITTEST_SOURCES"])
	for i in ["HOME", "USERPROFILE", "APPDATA"]:
		if os.environ.get(i, "") :
			myenv["ENV"][i] = os.environ[i]
	check = myenv.Alias("check", checker, env.get("TEST_RUNNER", "") + checker[0].abspath)
	myenv.AlwaysBuild(check)
